plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'au.com.dius.pact' version '4.3.5'
	id "org.springdoc.openapi-gradle-plugin" version "1.3.3" // Used for: generateOpenApiDocs
	id "com.github.johnrengelman.processes" version "0.5.0"
	id 'org.unbroken-dome.test-sets' version '4.0.0'
}

group = 'com.nathandeamer'
version = '0.0.1-SNAPSHOT'
sourceCompatibility=17

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2021.0.1")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	implementation 'org.springdoc:springdoc-openapi-ui:1.6.6'

	testImplementation 'au.com.dius.pact.provider:junit5:4.2.20'

	runtimeOnly 'com.h2database:h2'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

def getGitBranch = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

testSets {
	pactTest {
		dirName = 'pact-test'
	}
}

static def getOrDefault(env, defaultVal) {
	def val = System.getenv(env)
	if (val == null || val.isEmpty()) {
		val = defaultVal
	}
	return val
}

pactTest {
	useJUnitPlatform()
	filter {
		setFailOnNoMatchingTests(false) // There won't always be consumer tests or provider tests
	}

	systemProperties System.properties // Important. Allow -D system properties for pact to be passed in.

	if (System.getProperty('pactPublishResults') == 'true') {
		systemProperty 'pact.provider.version', getGitHash()
		systemProperty 'pact.provider.branch', getGitBranch()
		systemProperty 'pact.provider.tag', getGitBranch()
		systemProperty 'pact.verifier.buildUrl', getOrDefault("CIRCLE_BUILD_URL", "")
		systemProperty 'pact.verifier.publishResults', 'true'
	}
}

test {
	useJUnitPlatform()
}